buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.7.3' apply false
    id 'com.github.ben-manes.versions' version '0.15.0' apply false
    id 'me.tatarka.retrolambda' version '3.7.0' apply false
    id 'com.github.hierynomus.license' version '0.14.0' apply false
    id 'net.nemerosa.versioning' version '2.6.1' apply false
    id 'org.sonarqube' version '2.5'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'net.nemerosa.versioning'


    repositories {
        jcenter()
        mavenCentral()
        maven { url 'http://sevntu-checkstyle.github.com/sevntu.checkstyle/maven2' }
    }

    if (JavaVersion.current().isJava8Compatible()) {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }

    apply from: rootProject.file('gradle/code-coverage.gradle')
    apply from: rootProject.file('gradle/checkstyle.gradle')
    apply from: rootProject.file('gradle/findbugs.gradle')
    apply from: rootProject.file('gradle/javafx.gradle')

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.incremental = true
    }
}
